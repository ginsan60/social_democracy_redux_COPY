title: Military Relations
view-if: military_relations_timer <= 0 and not victory_republic
new-page: true
tags: pres_affairs
is-card: true
card-image: img/schleicher.jpeg
on-departure: military_relations_timer += 5

= Military Relations

The office of presidency is by precedent and nature closely intertwined with the Reichswehr, with the Ebert-Groener pact of 1918 and 1919, whereby the army would support the government of Ebert in return for retaining its status as a state within a state and in return for the suppression of communist movements, setting the tone of military matters since and creating an unfortunate atmosphere of scheming. 

[? if former_president == "Hindenburg" : Hindenburg took this much further, developing a camarilla of military men around him that gradually influenced his decisions more and more as time went by, and irrevocably intertwining presidential and military matters with this precedent. ?][? if former_president == "Marx" : This relationship continued under Marx, who was forced to balance not just his civilian duties as president but military ones as well to prevent a coup and remain on good terms with the army. ?]

Relations with the Military: [+ army_relation : relationships +]

#- @camarilla: Interfere in the internal politics of the army.
- @allow_rearmament: Allow secret reamament.
- @surrender_reichswehr: Sack Leber from the Reichswehr Ministry.
#- @sack_breitscheid: Sack Breitscheid as chancellor.
- @unban_sh: Unban the Stalhelm.
- @ban_rfb: Ban the RFB.
- @reichsexekute_kpd: Reichsexekute left-wing state governments.
- @root: No, we should not do any of these things.
- @easy_discard

@allow_rearmament
choose-if: reichswehr_minister_party != "SPD"
unavailable-subtitle: The rest of the SPD would know, as Leber is Reichswehr Minister.
on-arrival: army_relation += 10; reichswehr_loyalty += 0.05 if reichswehr_loyalty < 0.5; west_relation -= 2; east_relation -= 2; reichswehr_militancy += 0.15; sh_militancy += 0.05; pacifism -= 1; nationalism += 3; pro_republic -= 3; left_dissent += 5

We allow the Reichswehr to secretly rearm, increasing its strength, and, seemingly though to a lesser extent, the strength of the Stalhelm. Though it is a secret, the Entente countries nonetheless have their suspicions, as does our party left.

@surrender_reichswehr
view-if: reichswehr_minister_party == "SPD"
on-arrival: army_relation += 5; workers_spd -= 3; new_middle_spd -= 3; old_middle_spd -= 3; rural_spd -= 3; catholics_spd -= 1; unemployed_spd -= 3; reichswehr_minister_party = "I"; left_dissent += 5; center_dissent += 5; labor_dissent += 5; neorevisionist_dissent += 5; kpd_coalition_dissent += 3 if in_left_front or in_popular_front

We pressure Leber to resign, allowing the Reichswehr to reassert control over its own ministry. This pleases them, but has embarassed and upset the SPD as a whole.

@unban_sh
view-if: ((not presidential_powers) or (interior_minister_party == "SPD")) and sh_banned
on-arrival: sh_banned = 0; army_relation += 5; sh_militancy += 0.05; sh_strength += 20; coup_progress -= 1; kpd_relation -= 5; kpd_relation -= 5 if spd_in_government; left_dissent += 10 if spd_in_government; center_dissent += 5 if spd_in_government; labor_dissent += 5 if spd_in_government; neorevisionist_dissent += 5 if spd_in_government

We have unbanned the Stalhelm, allowing it to continue its organization and militarization. [? if spd_in_government : This has come under criticism from the rest of the SPD, which is under increasing pressure to withdraw from government. ?]

@ban_rfb
view-if: ((not presidential_powers) or (interior_minister_party == "SPD")) and not rfb_banned
on-arrival: rfb_banned = 1; army_relation += 5; rfb_militancy -= 0.05; rfb_strength -= 20; coup_progress -= 1; kpd_relation -= 5; kpd_relation -= 5 if spd_in_government; left_dissent += 5 if spd_in_government

We have banned the RFB, satisfying and fulfilling one of the core conditions of the Ebert-Groener Pact.

@reichsexekute_kpd
choose-if: not spd_in_government or reformist_strength >= 50
unavailable-subtitle: The SPD in government refuses to allow this.
on-arrival: kpd_relation -= 10 if spd_in_government; left_dissent += 20 if spd_in_government; rb_strength *= 0.95; left_strength += 10 if not spd_in_government; reformist_dissent += 10 if not spd_in_government; capital_strike_progress -= 2; coup_progress -= 2; rb_militancy -= 0.1; rfb_militancy -= 0.05; workers_spd *= 0.9
view-if: kpd_prussia or red_thuringia or red_saxony or in_hamburg_left

As part of the Ebert-Groener Pact, we are expected to dismantle communist infiltration thoroughly.

- #reichsexekutions_left

@reichsexekute_hamburg
view-if: in_hamburg_left
tags: reichsexekutions_left
title: Reichsexekute Hamburg.
subtitle: This will anger the SPD!
on-arrival: in_hamburg_left = 0; in_spd_hamburg = 1

We have performed a *Reichsexekution* against the government of Hamburg, removing it of KPD ministers.

@reichsexekute_thuringia
view-if: red_thuringia
tags: reichsexekutions_left
title: Reichsexekute Thuringia.
subtitle: This will anger the SPD!
on-arrival: red_thuringia = 0

We have performed a *Reichsexekution* against the government of Thuringia, removing it of KPD ministers.

@reichsexekute_saxony
view-if: red_saxony
tags: reichsexekutions_left
title: Reichsexekute Saxony.
subtitle: This will anger the SPD!
on-arrival: red_saxony = 0

We have performed a *Reichsexekution* against the government of Saxony, removing it of KPD ministers.

@reichsexekute_prussia
view-if: kpd_prussia
tags: reichsexekutions_left
title: Reichsexekute Prussia.
subtitle: This will anger the SPD!
on-arrival: kpd_prussia = 0

We have performed a *Reichsexekution* against the government of Prussia, removing it of KPD ministers.