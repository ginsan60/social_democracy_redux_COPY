title: A New Government Forms
subtitle: [+ president +] is to appoint a new government
view-if: year == 1926 and locarno_seen
new-page: true
max-visits: 1
tags: event
go-to: marx_chancellor if president == "Marx"; hindenburg_chancellor if president == "Hindenburg"; braun_chancellor if president == "Braun"

= A New Government Forms

@hindenburg_chancellor

Since the fall of the Luther cabinet, there has been much speculation on what government may take its place. One idea has gained surprising momentum, however.

- @grand_coalition_real: A "Grand Coalition"?

@grand_coalition_real
subtitle: SPD + Z + BVP + DDP + DVP (61%)

The bourgeois parties have reached out to us to form a grand coalition - even Hindenburg has expressed willingness to the idea should a clear working basis be established. To this end, the Democrat Koch-Weser has been entrusted with cabinet negotiation, and attempts to gain our support.

However, entering such a government in a time of increasing economic uncertainty may end the trend of our increasing voteshare, and bolster the communists as an alternative rather than us. Furthermore, the bourgeois parties are unwilling to meet our demand for new elections.

- @accept_groko: Form the Grand Coalition!
- @refuse_groko: Refuse the offer.

@accept_groko
choose-if: reformist_strength > left_strength+labor_strength and not princes_initiated
unavailable-subtitle: The party is opposed.
on-arrival: left_dissent += 5; labor_dissent += 5; workers_spd -= 3; unemployed_spd -= 5; workers_kpd += 2; unemployed_kpd += 2; old_middle_dvp -= 2; chancellor = "Koch-Weser"; chancellor_party = "DDP"; in_grand_coalition = 1; spd_in_government = 1; leverage = spd_r; leverage -= 5; princes_time = time+3 if not princes_initiated
go-to: election_1928.ministries

We have decided to enter government as per the seemingly sincere wishes of the bourgeois. Now we must decide which ministries to take.

[? if not princes_initiated : Meanwhile, the communists, seeing that we will seemingly not be joining their referendum, have gone ahead with collecting signatures. ?]

@refuse_groko
on-arrival: dvp_relation -= 3; z_relation -= 2; ddp_relation -= 2

With a Grand Coalition out of the picture, a short negotiation period between the bourgeois parties follows. President Hindenburg then announces the formation of a new government.

- @luther_second: A second Luther cabinet.

@luther_second
subtitle: Z + BVP + DDP + DVP (35%)
on-arrival: budget -= 2; upper_tax_rates -= 1; lower_tax_rates -= 1; unemployed -= 1 if unemployed >= 5; inflation += 0.4; economic_growth += 0.4; socialism -= 1

Luther is reappointed as chancellor, this time in a minority government of the Center, Democratic, People's and Bavarian People's parties. As a minority government, it is unlikely to last long, and de-facto relies on us as we could bring it down at any moment. Doing so without a justification, however, could hurt our own support and drive the bourgeois back into the hands of the DNVP.

Meanwhile, one of the first things the government does is try to cut taxes in order to stimulate the economy. Only time will tell if this will be effective.

- @tolerate_luther: Let Luther govern, for now.
#- @vonc_luther: Call a motion of no confidence with the KPD.

@tolerate_luther
on-arrival: spd_toleration = 1; dvp_relation += 3; z_relation += 4; ddp_relation += 4; marx_relation += 3;

We will see how things play out. For now, the SPD will not move against the government. [? if not princes_initiated: However, the continued position of the SPD outside of government, and the disappearance of a Grand Coalition as a real possibility for the time being opens up once more the possibility of collaborating with the Communists in their initiative for the expropriation of the princes. ?]

- @back_referendum: Back the communist initiative for a referendum.
- @refuse_referendum: We can still resolve this issue within the Reichstag.

@back_referendum
view-if: not princes_initiated
on-arrival: princes_initiated = 1; princes_time = time+2; kpd_relation += 5; dvp_relation -= 3; old_middle_spd -= 2; new_middle_spd -= 2; marx_relation -= 5; 

As we will be in opposition for the foreseeable future, there is no real reason to let the communists be the only party to benefit from the support for expropriation. Though there is no way we could directly back the communist suggestion, the ADGB has mediated a bill that both parties are able to agree on, and preparations for referendum are underway.

@refuse_referendum
view-if: not princes_initiated
on-arrival: princes_time = time+3; left_dissent += 5; workers_spd -= 4; workers_kpd += 4; unemployed_spd -= 5; unemployed_kpd += 5; rural_kpd += 1; dvp_relation += 5; z_relation += 5; ddp_relation += 5; marx_relation += 5

We continue to refuse to back the communist initiative for a referendum. Though this leaves the KPD in a strong position to gain, it leaves open the possibility of resolving the issue with a compromise in the Reichstag.

@vonc_luther
choose-if: left_strength > center_strength+reformist_strength
unavailable-subtitle: The party is opposed.
on-arrival: marx_relation -= 10; workers_spd -= 2; new_middle_spd -= 4; old_middle_spd -= 3; z_relation -= 5; ddp_relation -= 5; dvp_relation -= 5; kpd_relation += 5; reformist_dissent += 10; left_strength += 10

# todo: DNVP back in gov?


@marx_chancellor

Since the fall of the Luther cabinet, there has been much speculation on what government may take its place. One idea has gained surprising momentum, however.

- @grand_coalition_real2: A "Grand Coalition"?

@grand_coalition_real2
subtitle: SPD + Z + BVP + DDP + DVP (61%)

The bourgeois parties have reached out to us to form a grand coalition under the encouragement of Wilhelm Marx, who has floated the democrat Koch-Weser for the role. He has now turned to us for support in this cabinet.

However, entering such a government in a time of increasing economic uncertainty may end the trend of our increasing voteshare, and bolster the communists as an alternative rather than us. Furthermore, the DVP seems reluctant to meet our demand for new elections.

- @accept_groko2: Form the Grand Coalition!
- @refuse_groko2: Refuse the offer.

@accept_groko2
choose-if: reformist_strength+(marx_relation/2) > left_strength+labor_strength and not princes_initiated
unavailable-subtitle: The party is opposed.
on-arrival: left_dissent += 5; labor_dissent += 5; workers_spd -= 3; unemployed_spd -= 5; workers_kpd += 2; unemployed_kpd += 2; old_middle_dvp -= 3; old_middle_dnvp += 1; marx_relation += 5; chancellor = "Koch-Weser"; chancellor_party = "DDP"; in_grand_coalition = 1; spd_in_government = 1; leverage = spd_r; leverage -= 5; princes_time = time+3 if not princes_initiated
go-to: election_1928.ministries

We have decided to enter government as per the seemingly sincere wishes of the bourgeois. Now we must decide which ministries to take.

[? if not princes_initiated : Meanwhile, the communists, seeing that we will seemingly not be joining their referendum, have gone ahead with collecting signatures. ?]

@refuse_groko2
on-arrival: dvp_relation -= 3; z_relation -= 2; ddp_relation -= 2; marx_relation -= 5

With a Grand Coalition out of the picture, a short negotiation period between the bourgeois parties follows.

- @luther_second2: A second Luther cabinet.

@luther_second2
subtitle: Z + BVP + DDP + DVP (35%)
on-arrival: budget -= 2; upper_tax_rates -= 1; lower_tax_rates -= 1; unemployed -= 1 if unemployed >= 5; inflation += 0.4; economic_growth += 0.4; socialism -= 1

Luther is reappointed as chancellor, this time in a minority government of the Center, Democratic, People's and Bavarian People's parties. As a minority government, it is unlikely to last long, and de-facto relies on us as we could bring it down at any moment. Doing so without a justification, however, could hurt our own support and drive the bourgeois back into the hands of the DNVP.

Meanwhile, one of the first things the government does is try to cut taxes in order to stimulate the economy. Only time will tell if this will be effective.

- @tolerate_luther: Let Luther govern, for now.
#- @vonc_luther: Call a motion of no confidence with the KPD.

@braun_chancellor

Since the fall of the Luther cabinet, there has been much speculation on what government may take its place. One idea has gained surprising momentum, however.

- @grand_coalition_real3: A "Grand Coalition"?

@grand_coalition_real3
subtitle: SPD + Z + BVP + DDP + DVP (61%)

The bourgeois parties have reached out to us to form a grand coalition, backed in doing so by Braun, who has floated the democrat Koch-Weser for the role following the DVP's rejection of a social democrat for the role. He has now reached out to us and the DVP for support in this cabinet.

However, entering such a government in a time of increasing economic uncertainty may end the trend of our increasing voteshare, and bolster the communists as an alternative rather than us. Furthermore, the DVP seems reluctant to meet our demand for new elections, or even join the government at all.

- @accept_groko3: Form the Grand Coalition!
- @refuse_groko3: Refuse the offer.

@accept_groko3
choose-if: dvp_relation >= 50 and not princes_initiated
unavailable-subtitle: The DVP is opposed.
on-arrival: left_dissent += 5; workers_spd -= 2; unemployed_spd -= 2; workers_kpd += 1; unemployed_kpd += 1; old_middle_dvp -= 4; old_middle_dnvp += 2; chancellor = "Koch-Weser"; chancellor_party = "DDP"; in_grand_coalition = 1; spd_in_government = 1; leverage = spd_r; leverage -= 5; princes_time = time+3 if not princes_initiated
go-to: election_1928.ministries

We have decided to enter government as per the seemingly sincere wishes of the bourgeois. Now we must decide which ministries to take.

[? if not princes_initiated : Meanwhile, the communists, seeing that we will seemingly not be joining their referendum, have gone ahead with collecting signatures. ?]

@refuse_groko3
on-arrival: dvp_relation -= 1; z_relation -= 1; ddp_relation -= 1

With a Grand Coalition out of the picture, a short negotiation period between the bourgeois parties follows.

- @luther_second3: A second Luther cabinet.

@luther_second3
subtitle: Z + BVP + DDP + DVP (35%)
on-arrival: budget -= 2; upper_tax_rates -= 1; lower_tax_rates -= 1; unemployed -= 1 if unemployed >= 5; inflation += 0.4; economic_growth += 0.4; socialism -= 1

Luther is reappointed as chancellor, this time in a minority government of the Center, Democratic, People's and Bavarian People's parties. As a minority government, it is unlikely to last long, and de-facto relies on us as we could bring it down at any moment. Doing so without a justification, however, could hurt our own support and drive the bourgeois back into the hands of the DNVP.

Meanwhile, one of the first things the government does is try to cut taxes in order to stimulate the economy. Only time will tell if this will be effective.

- @tolerate_luther: Let Luther govern, for now.
#- @vonc_luther: Call a motion of no confidence with the KPD.