title: The Right is expelling us from their government!
subtitle: We have angered our coalition partners too much...
new-page: true
on-arrival: tried_supporting_kpd = 0; weimar_dissent += 1 if weimar_plan_adopted; marx_relation -= 5
view-if: chancellor_party != "SPD" and chancellor_party != "KPD" and chancellor != "Koch-Weser" and chancellor != "Ossietzky" and spd_in_government = 1 and ((in_grand_coalition = 1 or in_popular_front = 1 or in_minority_government = 1) and (coalition_dissent >= 3 or (chancellor == "Scholz" and coalition_dissent >= 2))) or (in_weimar_coalition = 1 and coalition_dissent >= 4) and spd_r < 50
tags: event
go-to: menu if chancellor != "Wirth"; wirth_resign_hindenburg if chancellor == "Wirth" and (president == "Hindenburg" or president == "Adenauer" or president == "Gessler" or president == "Eckener" or president == "Marx"); wirth_resign_braun if chancellor == "Wirth" and (president == "Braun" or president == "Schumacher" or president == "Juchacz")

= Cabinet Reshuffled

@menu

[+ chancellor +] has announced his intentions to [+ president +] to reshuffle the cabinet, sacking and replacing all current SPD ministers and replacing them with bourgeois ones. [? if bourgeois_coalition < 50 and right_coalition < 50 : Luckily, they lack a majority without us, so we can bring them down with a simple motion of no confidence. ?]

- @try_assuage: Try to assuage our partners.
- @right_coalition: The DNVP replaces us in government.
- @minority_bruning: Brüning's cabinet survives as a minority government.
- @breitscheid_vonc: Call a constructive motion of no confidence with the KPD!
- @sack_bruning: [+ president +] will call snap elections.
- @bourgeois_coalition: The bourgeois have a lone majority.
- @tolerate: Let the reshuffle happen, and tolerate the new minority government.
- @no_tolerate: Bring down this new backstabbing government!

@try_assuage
choose-if: chancellor != "Scholz" or dvp_relation >= 55
unavailable-subtitle: Chancellor Scholz sees no room for compromise.

How do we assuage our partners?

- @give_up_prussia: Give up on leadership of Prussia.
- @austerity: Enact a massive austerity plan.
- @resources: Use our resources to reduce dissent within the coalition.
- @menu: Never mind. What are our options?

@give_up_prussia
view-if: spd_prussia == 1
on-arrival: coalition_dissent = 0; spd_prussia = 0; prussian_government = "I"; reformist_dissent += 10; centrist_dissent += 10; labor_dissent += 10; left_dissent += 5; workers_spd -= 5; rural_spd -= 5; 

Fine. We no longer control the government of Prussia. The vote has been called off.

@austerity
view-if: welfare >= 0
on-arrival: coalition_dissent = 0; workers_spd -= 10; unemployed_spd -= 10; workers_kpd += 7; workers_nsdap += 3; unemployed_kpd += 5; unemployed_nsdap += 5; left_dissent += 15; center_dissent += 10; labor_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployed += 3; kpd_coalition_dissent += 1; kpd_relation -= 10 if in_popular_front

Fine. We enacted a massive austerity program to cut welfare, in order to maintain the stability of our coalition. The vote has been called off.

@resources
choose-if: resources >= 3
unavailable-subtitle: We do not have enough resources.
subtitle: -3 resources
on-arrival: resources -= 3; coalition_dissent -= 1;

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.

@right_coalition
view-if: right_coalition >= 50 and bourgeois_coalition < 50
choose-if: right_coalition_failed != 1 and (year < 1929 and (year < 1928 or month < 8))
unavailable-subtitle: The DNVP isn't willing to enter government.
go-to: election_1928.right_coalition_2

@bourgeois_coalition
view-if: bourgeois_coalition >= 50
go-to: election_1928.let_it_form

@minority_bruning
view-if: constructive_vonc and ((right_coalition < 50 or right_coalition_failed or year >= 1929 or (year >= 1928 and month >= 8)) and bourgeois_coalition < 50)
on-arrival: spd_in_government = 0; spd_toleration = 0; minority_government = 1; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "Z"; labor_minister_party = "Z"; reichswehr_minister_party = ddp_name; economic_minister_party = "Z"; finance_minister_party = "Z"; agriculture_minister_party = "Z"

Due to our own constitutional reforms, we cannot bring [+ chancellor +] down. We will have to prepare for the next election to get our chance.

@breitscheid_vonc
view-if: constructive_vonc and ((right_coalition < 50 or right_coalition_failed or year >= 1929 or month >= 8) and bourgeois_coalition < 50) and (left_coalition >= 50 or kpd_relation >= 50)
choose-if: left_coalition >= 50 and kpd_relation >= 50
unavailable-subtitle: [? if left_coalition < 50 : We do not have a majority in favor of Breitscheid. ?][? if kpd_relation < 50 : The Communists are unwilling. ?]
on-arrival: old_chancellor = chancellor; in_left_front = 1; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; chancellor_party = "SPD"; chancellor = "Breitscheid"; labor_minister_party = "SPD"; interior_minister_party = "SPD"; finance_minister_party = "SPD"; economic_minister_party = "SPD"; justice_minister_party = "SPD"; foreign_minister_party = "SPD"; agriculture_minister_party = "SPD"; reichswehr_minister_party = "SPD"

We have succesfully passed a motion of no confidence to replace [+ old_chancellor +] with Breitscheid. We are now ruling in a united front with the KPD.

@sack_bruning
view-if: constructive_vonc and president == "Braun" or president == "Schumacher" or president == "Juchacz"
choose-if: presidential_powers != 1
unavailable-subtitle: We don't have that power anymore. You really screwed us here with your reforms, you know that?
call: set_next_election_time
on-arrival: time_to_election = 3; weimar_dissent += 1 if weimar_plan_adopted; spd_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; spd_toleration = 0; foreign_minister_party = "I"; interior_minister_party = "I"; justice_minister_party = "I"; labor_minister_party = "I"; reichswehr_minister_party = "I"; economic_minister_party = "I"; finance_minister_party = "I"; agriculture_minister_party = "I"; coalition_dissent = 0; pro_republic -= 5; old_middle_spd -= 3; new_middle_spd -= 2; workers_spd -= 3 if pro_republic < 50; workers_spd -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50

We have sacked [+ chancellor +] and called for new elections for [+ next_election_month : month +] [+ next_election_year +].

@tolerate
view-if: not constructive_vonc
choose-if: (right_coalition < 50 or right_coalition_failed or year >= 1929 or month >= 8) and bourgeois_coalition < 50
unavailable-subtitle: They don't need us.
on-arrival: left_dissent += 10; labor_dissent += 5; center_dissent += 5; workers_spd -= 3
go-to: election_1928.toleration_bruning

Many in the party are incredulous. We have just been expelled from government, and our reaction is to allow this government to govern?

@no_tolerate
view-if: not constructive_vonc
choose-if: ((right_coalition < 50 or right_coalition_failed or year >= 1929 or month >= 8) and bourgeois_coalition < 50)
unavailable-subtitle: They don't need us.
go-to: reichstag_dissolved if ((chancellor != "Brüning" and chancellor != "Scholz") or (year >= 1931 or confrontation_seen or president != "Hindenburg")); dnvp_tolerate if ((chancellor == "Brüning" or chancellor == "Scholz") and (year < 1931 and not confrontation_seen and president == "Hindenburg"))
on-arrival: weimar_dissent += 1 if weimar_plan_adopted; spd_in_government = 0; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; spd_toleration = 0; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; labor_minister = "Stegerwald"; interior_minister = "Wirth"; finance_minister = "Moldenhauer"; economic_minister = "Curtius"; justice_minister = "Koch-Weser"; foreign_minister = "Stresemann"; agriculture_minister = "Dietrich"; reichswehr_minister = "Groener";  coalition_dissent = 0; pro_republic -= 5; old_middle_spd -= 3; new_middle_spd -= 2; workers_spd -= 3 if pro_republic < 50; workers_spd -= 4 if pro_republic < 40; old_middle_nsdap += 4 if pro_republic < 50; new_middle_nsdap += 3 if pro_republic < 50; welfare -= 2;

The vote succeeds, and their government has resigned. Chancellor [+ chancellor +] remains as the head of an acting government, consisting solely of the bourgeois parties. He immediately proceeds to cut welfare spending substantially.

@reichstag_dissolved
call: set_next_election_time
on-arrival: time_to_election = 3

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@dnvp_tolerate
on-arrival: dnvp_toleration = 1

The DNVP is split, as what remains of its moderate wing refuses to vote against [+ chancellor +] following Schiele (a high-ranking member of the DNVP) being included in cabinet.

@wirth_resign_braun
call: set_next_election_time
on-arrival: time_to_election = 3; chancellor = "Wels"; chancellor_party = "SPD"; spd_caretaker = 1; in_grand_coalition = 0; in_weimar_coalition = 0; in_popular_front = 0; spd_toleration = 0; foreign_minister_party = "I"; interior_minister_party = "I"; justice_minister_party = "I"; labor_minister_party = "I"; reichswehr_minister_party = "I"; economic_minister_party = "I"; finance_minister_party = "I"; agriculture_minister_party = "I"; coalition_dissent = 0; pro_republic -= 5;

Unable to bear the stress of keeping the coalition together, Wirth has announced his intentions to resign as chancellor.

[+ president +] has appointed as replacement Wels, who is immediately faced with a successful motion of no confidence. New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@wirth_resign_hindenburg
on-arrival: chancellor = "Brüning"; chancellor_party = "Z"

Unable to bear the stress of keeping the coalition together, Wirth has announced his intentions to resign as chancellor.

[+ president +] appoints Brüning from the Center Party as replacement, who immediately reforms the government.

- @menu: Brüning sacks our ministers.
