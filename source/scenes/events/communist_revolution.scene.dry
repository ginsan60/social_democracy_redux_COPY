title: [+ month : month +] Revolution
subtitle: The Communists are launching a revolution!
view-if: ((kpd_votes+sapd_votes+rfb_force >= 40) or (kpd_votes+sapd_votes+spd_votes+rfb_force+rb_force >= 80 and kpd_relation >= 70 and left_strength >= 50 and not spd_in_government)) and (chancellor == "Schleicher" or chancellor == "Papen" or chancellor == "Groener" or chancellor_party == "NSDAP" or chancellor_party == "DNVP" or (in_emergency_government and kpd_relation < 30 and kpd_no_confidence)) and (not supreme_soviet or kpd_relation < 25 and kpd_influence < 2) and not in_left_front and not in_popular_front
new-page: true
tags: event
max-visits: 1
on-arrival: {!
Q.communist_strength = Q.rfb_force;
Q.normalized_workers_spd = 100*Q.workers_spd/(Q.workers_spd + Q.workers_kpd + Q.workers_nsdap + Q.workers_other + Q.workers_dnvp + Q.workers_z);
Q.normalized_workers_kpd = 100*Q.workers_kpd/(Q.workers_spd + Q.workers_kpd + Q.workers_nsdap + Q.workers_other + Q.workers_dnvp + Q.workers_z);
Q.normalized_workers_total = Q.normalized_workers_kpd;
Q.communist_strength += Q.normalized_workers_total*(2 - (Q.unemployed*(1-(Q.unemployed_kpd_normalized/100)))/100)*2.5;
Q.communist_strength += Q.rb_force*(Q.left_strength/100);

Q.harzburg_strength = Q.sa_force;
Q.harzburg_strength += Q.sh_force;
Q.harzburg_strength += Q.prussia_force*(1-Q.prussian_police_loyalty);
Q.harzburg_strength += Q.rural*(1-((Q.rural_spd_normalized+Q.rural_kpd_normalized)/100));
!}

= [+ month : month +] Revolution

Sick and tired of the [+ chancellor +] government and viewing it as the greatest obstacle to the victory of the working class, and further viewing the conditions favorable for the abolition of capitalism by a communist revolution, the KPD has seemingly commenced the revolutionary process. Out of nowhere one [+ month : month +] morning, a call to general strike is published while the RFB storms key infrastructure points across the country, attempting especially to secure control over arms with which to distribute to local workers. Streetfighting erupts as militias and police forces opposed to the communists attempt to put down their uprisings while further more expansive ones are launched across the country.

Factories are occupied through an initiative of the communist trade unions, and RFB-backed worker councils formed within them. Local government offices are seized and occupied, leaving the inprompteau worker councils as the only form of governance, while within those councils KPD and RFB officials are elected to direct the ongoing revolution. 

- @spd_supports: The SPD stands with the revolution!
- @what: This is madness!

@spd_supports
choose-if: left_strength >= reformist_strength+center_strength and not spd_in_government and rb_militancy >= 0.2
unavailable-subtitle: This is madness.
on-arrival: spd_revolution = 1; kpd_relation += 100; z_relation -= 100; ddp_relation -= 100; dvp_relation -= 100; rb_strength *= (left_strength/100); communist_strength += (rb_force*(1-(left_strength/100)))/2; communist_strength += ((1-(labor_dissent/100))*normalized_workers_spd*(2 - unemployed/100)*2.5); reformist_dissent += 100; center_dissent += 50
go-to: kpd_survival if communist_strength >= harzburg_strength; kpd_crushed if communist_strength < harzburg_strength

We mobilize the Reichsbanner against the reactionary militias to defend the worker councils, and join the call to general strike. What's left of our party's right-wing leave, including the Reichsbanner. 

@what
go-to: kpd_survival if communist_strength >= harzburg_strength; kpd_crushed if communist_strength < harzburg_strength

@kpd_survival

The streetfighting in the streets reaches levels one would expect in a civil war before eventually, the communists seem to remain standing. Police stations are stormed across the country, red flags raised above while forces of the SA and Stalhelm driven out of the cities and into the countryside. Arms are distributed from seized armories to local radical workers, organized into a new fledgling Red Army with one task: the smashing of the German bourgeois state apparatus, and the defense of the councils.

Reichswehr forces across the country are taken out of their barracks to quell the uprisings and restore order, only pre-empted in communist strongholds where barracks were stormed by local Red Army contingents. The failure of the bourgeois' first line of defense to suppress this revolutionary attempt has reinvigorated the communists' efforts.

Now, the question on everyone's minds will be answered - can the communists defend against the full force of the state?

- @defend_republic: The SPD stands with the republic against this "revolution"!
- @neutrality: It would be best to wait the chaos out.
- @kpd_vs_army: The state is nothing without the support of the workers!

@defend_republic
view-if: not spd_revolution
choose-if: chancellor != "Papen" and chancellor_party != "NSDAP" and chancellor_party != "DNVP"
unavailable-subtitle: What republic is there left to defend?
on-arrival: left_dissent += 100; rb_strength *= (1-(left_strength/100)); harzburg_strength += (rb_force*(1-(left_strength/100)))/2; harzburg_strength += ((1-(labor_dissent/100))*normalized_workers_spd*(2 - unemployed/100)*2.5);
go-to: kpd_vs_army

The SPD stands with the republic and with democracy against this Moscow-sponsored anti-democratic criminal uprising. We shall mobilize every force at our disposal to defeat the communists.

@neutrality
view-if: not spd_revolution
go-to: kpd_vs_army
choose-if: not spd_in_government
unavailable-subtitle: The communists are rising against us. We have no choice but to fight back.

By firmly taking a side we would split, as the communists want us to. We will wait the chaos out.

@kpd_vs_army
view-if: spd_revolution
on-arrival: harzburg_strength += army_force
go-to: kpd_victory if communist_strength > harzburg_strength; kpd_beaten if harzburg_strength >= communist_strength

@kpd_victory

The answer seems to be yes, as, somehow, the Red Army drive the Reichswehr forces back into their barracks which are then besieged and occupied one by one. The flames of civil war have been ignited and the communists are prepared to fight this uphill battle to bring about the dictatorship of the proletariat they so desire.

- @long_war: The war settles into fixed battle lines.
- @true_kpd_victory: The communists emerge victorious!
- @civil_war_if_spd: It is a civil war.

@long_war
view-if: not spd_revolution and (communist_strength*0.6) < harzburg_strength
on-arrival: long_war = 1

The initial phase of the action ends, and somehow, both sides remain standing.

This will not be a quick coup, but a long civil war. The nation will destroy itself before it can be rebuilt again.

- @game_over: Game over.

@true_kpd_victory
view-if: not spd_revolution and (communist_strength*0.6) >= harzburg_strength
on-arrival: capital_strike_progress += 50; works_councils += 3; economic_democracy += 3; time_to_election = 3; red_banner = 1; supreme_soviet = 1; workers_kpd += 25; new_middle_kpd += 10; unemployed_kpd += 25; president = "Th√§lmann"; nsdap_banned = 1; dne_banned = 1 if dne_formed; dne_reorganized = 0 if dne_reorganized; party_bans = 1; dnvp_banned = 1; dvp_banned = 1; other_banned = 1; ddp_banned = 1; z_banned = 1 if z_party_leader != "Joos"; sa_banned = 1; sh_banned = 1; constitutional_reform += 1; rfb_banned = 0; socialism += 25; chancellor = "Ulbricht"; chancellor_party = "KPD"; victory_republic = 1; unemployed_nsdap *= 0.2; new_middle_nsdap *= 0.3; old_middle_nsdap *= 0.4; workers_nsdap *= 0.2; rural_nsdap *= 0.5; old_middle_dnvp *= 0.4; rural_dnvp *= 0.5; new_middle_dnvp *= 0.2; sa_strength *= 0.2; sh_strength *= 0.2; sa_militancy *= 0.5; sh_militancy *= 0.5; rb_strength *= 0.8; rfb_strength *= 0.8; reichswehr_strength = 0
call: set_next_election_time

The Red Army descends upon Germany, picking off reactionary holdout after reactionary holdout in its crusade against capital. Eventually, it emerges unmistakeably victorious, most of Germany under its control. Elections to a supreme soviet are scheduled for [+ next_election_month : month +].

@civil_war_if_spd
view-if: spd_revolution
on-arrival: spd_rebel = 1
go-to: civil_war

@kpd_beaten

The answer seems to be no, as, predictably, the Reichswehr drive the Red Army forces back into their councils which are then besieged and occupied one by one. The flames of civil war have been ignited but the communists seem after all to not be ready for what is to come.

- @long_war2: The war settles into fixed battle lines.
- @kpd_crushed: The communists are crushed.
- @civil_war_if_spd: It is a civil war.

@long_war2
view-if: not spd_revolution and (harzburg_strength*0.6) < communist_strength
on-arrival: long_war = 1

The initial phase of the action ends, and somehow, both sides remain standing.

This will not be a quick coup, but a long civil war. The nation will destroy itself before it can be rebuilt again.

- @game_over: Game over.

@kpd_crushed
view-if: not spd_revolution and (harzburg_strength*0.6) >= communist_strength
on-arrival: rfb_banned = 1; rfb_strength *= 0.1; rfb_militancy *= 0.4; workers_kpd *= 0.4; unemployed_kpd *= 0.6; workers_spd *= 0.9; workers_spd *= 0.7 if spd_revolution; workers_nsdap += 10; unemployed_nsdap += 10; workers_dnvp += 10

The communists are predictably crushed. Worker councils are shuttered and leading conspirators executed. The RFB is banned, and so are most communist publications. The KPD's leaders are arrested - those that are not summarily executed, anyway.

